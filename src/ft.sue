	$NAME	<FILE TEST PROGRAM>

	JMP	FT

FILE1:	$LFDB	<      >
FILE2:	$LFDB	<      >

HELLO:	$MESS	<[FILE TEST]>
PROMPT:	$LMESS	<[FT] >
COMERR:	$MESS	<!! COMMAND NOT RECOGNISED>
LFDRER:	$MESS	<!! WRONG LFD REFERENCE>
MLOOK:	$LMESS	<[LOOK] >
LEX:	$MESS	< EXISTS >
LNEX:	$MESS	< DOES NOT EXIST >
LBK:	$MESS	<WITH BACKUP.>
LNBK:	$MESS	<WITHOUT BACKUP.>
LOPEN:	$MESS	< OPEN FOR >
LOI:	$MESS	<INPUT.>
LOO:	$MESS	<OUTPUT.>
LCLOSE:	$MESS	< CLOSED.>
LEOF:	$MESS	< END OF FILE.>
MOPENI:	$LMESS	<[OPENI] >
MOPENO:	$LMESS	<[OPENO] >
MCLOSE:	$LMESS	<[CLOSE] >
MREAD:	$LMESS	<[READ] >
MWRITE:	$LMESS	<[WRITE] >
MDELET:	$LMESS	<[DELETE] >
MRENAM:	$LMESS	<[RENAME] >
MINFNM:	$LMESS	<[INPUT FILENAME] >
MEXEC:	$LMESS	<[RUN] >

COMTBL:	.BYTE	'I, 'O, 'C, 'R, 'W, 'F, 'D, 'N, 'E
COMCEI:	.EVEN

TSTTBL:	.WORD	OPENI,	OPENO,	CLOSE
	.WORD	READ,	WRITE,	INFNM
	.WORD	DELETE,	RENAME,	EXEC
	.PAGE
;
;	R A D 5 0   T O   A S C I I
;
RATAB:	.BYTE	'$, '., '#, 0

R50ASC:	MOV	R2,	-(SP)
	MOV	R1,	-(SP)
	MOV	R0,	-(SP)
	MOV	(PSP)+,	R1
	MOV	#3,	R2
RASC1:	CLR	R0
	DIV	#50,	R0
	TST	R1
	BNE	RASC2
	MOV	#40,	R1
	BR	RASC5
RASC2:	CMP	R1,	#33
	BGE	RASC3
	ADD	#101-1,	R1
	BR	RASC5
RASC3:	CMP	R1,	#35
	BLT	RASC4
	ADD	#60-36,	R1
	BR	RASC5
RASC4:	MOVB	RATAB-33(R1), -(SP)
	MOV	(SP)+,	R1
RASC5:	MOV	R1,	-(PSP)
	MOV	R0,	R1
	SOB	R2,	RASC1
	MOV	(SP)+,	R0
	MOV	(SP)+,	R1
	MOV	(SP)+,	R2
	RTS	PC
;
;	P R I N T   F I L E N A M E
;
PFNAM:	$DCHAR	#'[
	TST	(R1)+
	MOV	(R1),	-(PSP)
	JSR	PC,	R50ASC
	MOV	-(R1),	-(PSP)
	JSR	PC,	R50ASC
	MOV	#6,	R3
PFNAM1:	$DCHAR	(PSP)+
	SOB	R3,	PFNAM1
	$DCHAR	#']
	RTS	PC
;
;	R E A D   L F D   R E F E R E N C E
;
RLFDR:	$INLIN
	MOV	(PSP)+,	R3
	MOV	#FILE1,	R1
	MOV	#FILE2,	R2
	CMP	R3,	#'1
	BEQ	RLFDR2
	CMP	R3,	#'2
	BEQ	RLFDR1
	$ERROR	#LFDRER
RLFDR1:	MOV	R2,	-(SP)
	MOV	R3,	R2
	MOV	(SP)+,	R3
RLFDR2:	RTS	PC
	.PAGE
;
;	L O O K
;
LOOK:	$DTEXT	#MLOOK
	JSR	PC,	PFNAM
	$LOOK	R1
	TST	(PSP)+
	BNE	LOOK1
	$DTEXT	#LNEX
	RTS	PC
LOOK1:	$DTEXT	#LEX
	MOV	#LNBK,	R2
	TST	(PSP)+
	BEQ	LOOK2
	MOV	#LBK,	R2
LOOK2:	$DTEXT	R2
	MOV	#LOO,	R2
	TST	(PSP)+
	BLT	LOOK4
	BGT	LOOK3
	$DTEXT	#LCLOSE
	RTS	PC
LOOK3:	MOV	#LOI,	R2
LOOK4:	$DTEXT	#LOPEN
	$DTEXT	R2
LOOK5:	MOV	#LEOF,	R2
	BRNEOF	R1,	LOOK6
	$DTEXT	R2
LOOK6:	RTS	PC
;
;	O P E N I
;
OPENI:	$DTEXT	#MOPENI
	JSR	PC,	PFNAM
	$OPENI	R1
	$INLIN
	RTS	PC
;
;	O P E N O
;
OPENO:	$DTEXT	#MOPENO
	JSR	PC,	PFNAM
	$OPENO	R1
	$INLIN
	RTS	PC
;
;	C L O S E
;
CLOSE:	$DTEXT	#MCLOSE
	JSR	PC,	PFNAM
	$CLOSE	R1
	$INLIN
	RTS	PC
;
;	R E A D
;
READ:	$DTEXT	#MREAD
	JSR	PC,	PFNAM
	$READ	R1
	TST	(PSP)+
	$INLIN
	RTS	PC
;
;	W R I T E
;
WRITE:	$DTEXT	#MWRITE
	JSR	PC,	PFNAM
	$WRITE	R1,	#'Z
	$INLIN
	RTS	PC
;
;	D E L E T E
;
DELETE:	$DTEXT	#MDELET
	JSR	PC,	PFNAM
	$DELET	R1
	$INLIN
	RTS	PC
;
;	R E N A M E
;
RENAME:	$DTEXT	#MRENAM
	JSR	PC,	PFNAM
	$RENAM	R1,	R2
	$INLIN
	RTS	PC
;
;	I N P U T   F I L E N A M E
;
INFNM:	$INLIN
	TST	(PSP)+
	$DTEXT	#MINFNM
	$INFNM	R1
	RTS	PC
;
;	E X E C U T E
;
EXEC:	$INLIN
	TST	(PSP)+
	$DTEXT	#MEXEC
	$RUN	R1
	.PAGE
;
;	F I L E   T E S T
;
FT:	$DTEXT	#HELLO
FTLOOP:	$DTEXT	#PROMPT
	MOV	#',,	R4
FT1:	CMP	R4,	#',
	BNE	FT4
	$INLIN
	MOV	(PSP)+,	R3
	MOV	#COMTBL, R0
FT2:	CMPB	R3,	(R0)+
	BEQ	FT3
	CMP	R0,	#COMCEI
	BNE	FT2
	$ERROR	#COMERR
FT3:	SUB	#COMTBL+1, R0
	ASL	R0
	JSR	PC,	RLFDR
	JSR	PC,	@TSTTBL(R0)
	MOV	(PSP)+,	R4
	BR	FT1
FT4:	$INEOL	R4
	MOV	#FILE1,	R1
	JSR	PC,	LOOK
	MOV	#FILE2,	R1
	JSR	PC,	LOOK
	BR	FTLOOP
	.END	FT
