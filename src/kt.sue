	$NAME	<KEYBOARD TEST>

	JMP	KT

EOL	=	13
SPACE	=	40

HELLO:	$MESS	<[KEYBOARD TEST]>
PROMPT:	$LMESS	<[KT] >

WARN:	$LMESS	<[WARNING]>
WARNI:	$LMESS	<MESSAGE INHIBITED...>
WARNU:	$LMESS	<MESSAGE UNINHIBITED...>
WMESS:	$MESS	<?? WARNING...>

CPOS:	.WORD	.CPOS-CPOS-2
	.ASCII	<EOL>/[CURSOR POSITION]/<EOL>
	.ASCII	/ENTER LINE WITH '!' IN IT/
.CPOS:
	.EVEN

DELAY:	.WORD	.DELAY-DELAY-2
	.ASCII	<EOL>/[DELAY]/<EOL>
	.ASCII	/DELAY OF 30 SECONDS WHEN CHARACTER TYPED./<EOL>
	.ASCII	/CHARACTER RETYPED WHEN DELAY FINISHED./
.DELAY:
	.EVEN

ECHO:	$LMESS	<[ECHO]>
ECHOT:	$LMESS	<ECHO ON>
ECHOF:	$LMESS	<ECHO OFF>

ESC:	.WORD	.ESC-ESC-2
	.ASCII	<EOL>/[ESCAPE INHIBIT]/<EOL>
	.ASCII	/ENTER TWO LINES WITH <ESC> IN THEM./
.ESC:
	.EVEN
ESCI:	$LMESS	<INHIBITED...>
ESCU:	$LMESS	<UNINHIBITED...>

BUFF:	.BLKB	^D256
	.PAGE
;
;	K E Y B O A R D   T E S T
;
KT:	$DTEXT	#HELLO
	$DTEXT	#WARN
	$SWRNI	#-1
	$DTEXT	#WARNI
	$WARN	#WMESS
	$SWRNI	#0
	$DTEXT	#WARNU
	$WARN	#WMESS
	;
	$DTEXT	#CPOS
	$DTEXT	#PROMPT
	MOV	#BUFF,	R0
KT1:	$INLIN
	MOVB	(PSP),	(R0)+
	CMP	(PSP)+,	#EOL
	BNE	KT1
	MOV	#BUFF,	R0
KT2:	MOVB	(R0)+,	R1
	$DCHAR	R1
	CMP	R1,	#'!
	BNE	KT3
	$CPOS
	MOV	(PSP)+,	R2
KT3:	CMP	R1,	#EOL
	BNE	KT2
	DEC	R2
	BLT	KT6
KT4:	DEC	R2
	BLT	KT5
	$DCHAR	#SPACE
	BR	KT4
KT5:	$DCHAR	#'^
KT6:	$DCHAR	#EOL
	;
	$DTEXT	#DELAY
	$DTEXT	#PROMPT
	$INCHR
	MOV	(PSP)+,	R1
	$DELAY	#^D30*^D50
	$DCHAR	R1
	$INEOL	R1
	;
	$DTEXT	#ECHO
	$DTEXT	#ECHOT
	$DTEXT	#PROMPT
	$INEOL	#'Z
	$DTEXT	#ECHOF
	$DTEXT	#PROMPT
	$STECO	#0
	$INEOL	#'Z
	$DCHAR	#EOL
	$STECO	#-1
	;
	$DTEXT	#ESC
	$DTEXT	#ESCI
	$DTEXT	#PROMPT
	$SESCI	#-1
	$INEOL	#'Z
	$DTEXT	#ESCU
	$DTEXT	#PROMPT
	$SESCI	#0
	$INEOL	#'Z
	$EXIT
	.END	KT
	


	
